var ast = require('lua.ast')
var Tag = require('lua.tag')
var TStmt = Tag.Stmt
var TExpr = Tag.Expr

var id = ast.Expr.id

var setmt = \tbl, mt, node ->
	return ast.Expr.call(id("setmetatable", node), {tbl, mt}, node)

var str = \txt, node ->
	return ast.Expr.string(txt, false, node)

var construct = \node ->
	var vks = {}
	var mt = ast.Expr.table({
		{ str(node.name.name, node), str("__metatable", node) }
	}, node)
	-- ctor table
	for i, v in ipairs(node.variants)
		var key = str(v.ctor.name, v.ctor)
		var params = {
			{ str(v.ctor.name, v.ctor), str("id", v.ctor) }
		}
		for n, p in ipairs(v.params)
			params[n+1] = { p }
		var tbl = ast.Expr.table(params, v.ctor)
		var val = ast.Expr.function(v.params, {}, {}, { ast.Stmt.return({setmt(tbl, id("mt", node), node)}, v.ctor) }, node)
		vks[i] = { val, key }
	
	var tbl = ast.Expr.table(vks, node)
	
	-- __call metamethod
	var idvar = id("var", node)
	var test = ast.Expr.binary('and'
		, ast.Expr.binary('=='
			, str("table", node)
			, ast.Expr.call(id("type", node), {idvar}, node)
			, node
		)
		, ast.Expr.binary('=='
			, str(node.name.name, node)
			, ast.Expr.call(id("getmetatable", node), {idvar}, node)
			, node
		)
		, node
	)
	var testfn = ast.Expr.function({id("_", node), idvar}, {}, {}, {
		ast.Stmt.if({test}, {
			{ ast.Stmt.return({ast.Expr.index(idvar, str("id", node), node)}, node) }
		}, nil, node)
	}, node)
	var callable = ast.Expr.table({
		{ testfn, str("__call", node) }
	}, node)
	
	var lambda = ast.Expr.function({id("mt", node)}, {}, {}, {ast.Stmt.return({setmt(tbl, callable, node)}, node)}, node)
	return ast.Stmt.local({node.name}, {}, {ast.Expr.call(lambda, {mt}, node)}, node)

var destruct = \node ->
	var vks = {}
	for i, v in ipairs(node.variants)
		var key = str(v.ctor and v.ctor.name or "_", v.ctor or node)
		var val = ast.Expr.function(v.params or {}, {}, {}, v.body, v.ctor or node)
		vks[i] = { val, key }
	var tbl = ast.Expr.table(vks, node)

	var idx = ast.Expr.binary('or'
		, ast.Expr.call(node.name, {node.arg}, node)
		, str("_", node)
		, node
	)
	var index = ast.Expr.index(tbl, idx, node)
	-- `do` needed to avoid ambiguous syntax, since we start with (...)
	return ast.Stmt.do({
		ast.Stmt.expression(ast.Expr.call(index, {ast.Expr.call(id("unpack", node), {node.arg}, node)}, node), node)
	}, node)
	
	
		
	
return \stmts ->

	var Stmt = {}
	var Expr = {}
	
	var visit_stmts = \nodes ->
		var list = {}
		for i, node in ipairs(nodes)
			var rule = Stmt[node.tag]
			list[i] = rule and rule(node) or node
		return list
	
	var visit_expr = \node ->
		var rule = Expr[node.tag]
		return rule and rule(node) or node
			
	var visit_exprs = \nodes ->
		var list = {}
		for i, node in ipairs(nodes)
			list[i] = visit_expr(node)
		return list

	--------- Expressions --------------------
	Expr[TExpr.Id] = \node ->
		if node.name == '@'
			node.name = 'self'
		return node
		
	Expr[TExpr.Function] = \node ->
		node.params = visit_exprs(node.params)
		node.body = visit_stmts(node.body)
		return node

	Expr[TExpr.Table] = \node ->
		var valkeys = {}
		for i, kv in ipairs(node.valkeys)
			valkeys[i] = {}
			valkeys[i][1] = visit_expr(kv[1])
			if kv[2]
				valkeys[i][2] = visit_expr(kv[2])
		node.valkeys = valkeys
		return node

	Expr[TExpr.Index] = \node ->
		node.obj = visit_expr(node.obj)
		node.idx = visit_expr(node.idx)
		return node

	Expr[TExpr.Property] = \node ->
		node.obj = visit_expr(node.obj)
		return node

	Expr[TExpr.Call] = \node ->
		node.func = visit_expr(node.func)
		var arg1 = node.args[1]
		if arg1 and arg1.name == '@'
			var func = node.func
			if not func.bracketed
				if func.tag == TExpr.Property
					table.remove(node.args, 1)
					return ast.Expr.invoke(func.obj, func.prop, node.args, node)

				else if func.tag == TExpr.Index
					-- simulate a wrapped self method call
					var obj = id('_self_', node)  -- lets hope we dont shadow parent scope variable
					node.args[1] = obj
					var body = {
						ast.Stmt.local({obj}, {}, {func.obj}, node)
						, ast.Stmt.return({
							ast.Expr.call(ast.Expr.index(obj, func.idx, node), node.args, node)
						}, node)
					}
					var lambda = ast.Expr.function({}, {}, {}, body, node)
					return ast.Expr.call(lambda, {}, node)

		node.args = visit_exprs(node.args)
		return node

	Expr[TExpr.Unary] = \node ->
		node.left = visit_expr(node.left)
		return node

	Expr[TExpr.Binary] = \node ->
		node.left = visit_expr(node.left)
		node.right = visit_expr(node.right)
		return node

	--------- Statements --------------------
	Stmt[TStmt.Expr] = \node ->
		node.expr = visit_expr(node.expr)
		return node

	Stmt[TStmt.Local] = \node ->
		node.vars = visit_exprs(node.vars)
		node.exprs = visit_exprs(node.exprs)
		return node

	Stmt[TStmt.Data] = \node ->
		-- if last variant has ctor
		if node.variants[#node.variants].ctor
			return construct(node)
		return destruct(node)

	Stmt[TStmt.Assign] = \node ->
		node.lefts = visit_exprs(node.lefts)
		node.rights = visit_exprs(node.rights)
		return node

	Stmt[TStmt.Do] = \node ->
		node.body = visit_stmts(node.body)
		return node

	Stmt[TStmt.If] = \node ->
		for i = 1, #node.tests
			node.tests[i] = visit_expr(node.tests[i])
			node.thenss[i] = visit_stmts(node.thenss[i])
		if node.elses
			node.elses = visit_stmts(node.elses)
		return node

	Stmt[TStmt.Forin] = \node ->
		node.exprs = visit_exprs(node.exprs)
		node.body = visit_stmts(node.body)
		return node

	Stmt[TStmt.Fornum] = \node ->
		node.first = visit_expr(node.first)
		node.last = visit_expr(node.last)
		if node.step
			node.step = visit_expr(node.step)
		node.body = visit_stmts(node.body)
		return node
		
	Stmt[TStmt.While] = \node ->
		node.test = visit_expr(node.test)
		node.body = visit_stmts(node.body)
		return node

	Stmt[TStmt.Repeat] = \node ->
		node.body = visit_stmts(node.body)
		node.test = visit_expr(node.test)
		return node

	Stmt[TStmt.Return] = \node ->
		node.exprs = visit_exprs(node.exprs)
		return node

	return visit_stmts(stmts)
