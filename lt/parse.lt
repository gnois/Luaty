-- Grammar spec refer to https://github.com/Microsoft/TypeScript/blob/master/doc/spec.md#38-specifying-types

var ast = require('lt.ast')
var ty = require('lt.type')
var operator = require('lt.operator')
var reserved = require('lt.reserved')

var Stmt = ast.Stmt
var Expr = ast.Expr
var Keyword = reserved.Keyword
var LJ_52 = false

var EndOfBlock = { TK_dedent = true, TK_else = true, TK_until = true, TK_eof = true, ['}'] = true, [')'] = true, [';'] = true, [','] = true }
var NewLine = { TK_newline = true }
-- expr kind
var Kind = { Expr = 'Expr', Var = 'Var', Property = 'Property', Index = 'Index', Call = 'Call', Union = 'Union' }


return \ls, warn ->
	-- disallow multiple statement on one line
	-- need to be in file scope coz parse_stmts() is recursive
	var stmted  -- keep the line number that we parsed

	-----  Error reporting ----------------
	var parse_error = \severe, em, ... ->
		var loc = ls.loc()
		warn(loc.line, loc.col, severe, string.format(em, ...))

	var err_warn = \em ->
		parse_error(2, "%s", em)

	var err_syntax = \em ->
		parse_error(3, "%s", em)

	var ls_value = ->
		if ls.value
			return "'" .. ls.value .. "'"

	var err_instead = \severe, em, ... ->
		var msg = string.format(em, ...)
		parse_error(severe, "%s instead of %s", msg, ls_value() or ls.astext(ls.token))

	var err_expect = \token ->
		err_instead(3, "%s expected", ls.astext(token))

	var err_symbol = ->
		var sym = ls.tostr(ls.token)
		var replace = {end = "<dedent>", local = "`var`", function = "\\...->", elseif = "`else if`", repeat = "`do`"}
		var rep = replace[sym]
		if rep
			parse_error(2, "use %s instead of '%s'", rep, sym)
		else
			parse_error(3, "unexpected %s", ls_value() or ls.astext(ls.token))


	-------- Helpers ---------------------------
	var is_keyword = ->
		var str = ls.tostr(ls.token)
		if Keyword[str]
			return str


	var lex_opt = \tok ->
		if ls.token == tok
			ls.step()
			return true
		return false


	var lex_check = \tok ->
		if ls.token ~= tok
			err_expect(tok)
		ls.step()


	var lex_match = \what, who, line ->
		if not lex_opt(what)
			if line == ls.line
				err_expect(what)
			else
				err_instead(3, "%s expected to match %s at line %d", ls.astext(what), ls.astext(who), line)
			return false
		return true


	var lex_str = ->
		var loc = ls.loc()
		var s
		if ls.token ~= 'TK_name' and (LJ_52 or ls.token ~= 'TK_goto')
			err_expect('TK_name')
			-- no longer exiting, so cannot be nil
			s = ls.tostr(ls.token)
		else
			s = ls.value
		ls.step()
		return s, loc


	-- return true only if a real indent is eaten
	var lex_indent = ->
		if NewLine[ls.token] and ls.next() == 'TK_indent'
			lex_opt('TK_newline')
			ls.step()
			return true
		return false


	-- return true only if a real dedent is eaten
	var lex_dedent = ->
		if ls.token == 'TK_dedent' or NewLine[ls.token] and ls.next() == 'TK_dedent'
			lex_opt('TK_newline')
			ls.step()
			return true
		return false


	var lex_opt_dent = \dented ->
		if not dented
			dented = lex_indent()
		else
			dented = not lex_dedent()
		lex_opt('TK_newline')
		return dented


	var skip_stmt = ->
		while not EndOfBlock[ls.token] and not NewLine[ls.token] and ls.token ~= 'TK_eof'
			ls.step()

	var skip_ends = ->
		while ls.token == ';' or ls.token == 'TK_end'
			err_symbol()
			ls.step()
		lex_opt('TK_newline')

	--- Type Declarations ------------------------------------------------
	var parse_type, type_unary, type_binary, type_basic 

	-- Only table type annotation can span multiple lines
	var type_tbl = \loc ->
		ls.step()  -- skip '{'
		var vks, n = {}, 0
		var dented = false
		while ls.token ~= '}'
			dented = lex_opt_dent(dented)
			-- catch common mistake that mess everything later
			if not dented and ls.token == 'TK_dedent'
				err_symbol()
				ls.step()

			-- in case the table ends immediately
			if ls.token == '}'
				break
			var val = parse_type()
			var key
			if ls.token == ':'
				ls.step()  -- skip ':'
				key = val
				val = parse_type()
				
			if key and not val
				err_instead(3, "value type expected in table type annotation")
			n = n + 1
			vks[n] = { val, key }  -- key can be nil

			dented = lex_opt_dent(dented)
			if not lex_opt(',')
				break

		if dented and not lex_dedent()
			err_instead(3, "%s expected to match %s at line %d", ls.astext('TK_dedent'), ls.astext('TK_indent'), loc.line)
		lex_match('}', '{', loc.line)
		return ty.tbl(vks)

	-- How to parse union of tuples?
	var type_tuple = \isparam ->
		var list, l = {}, 0
		if not (isparam and ls.token == ':' or ls.token == ']')
			do
				if ls.token == '...'
					ls.step()
					l = l + 1
					list[l] = parse_type(true)
					break
				else
					l = l + 1
					list[l] = parse_type()
					--``
					var which = "return"
					if isparam
						which = "parameter"
					parse_error(2, "invalid %s type %s", which, ls.astext(ls.token))
					``
			until not lex_opt(',')
		return ty.tuple(list)


	var type_func = \loc ->
		ls.step()  -- skip '['
		var params = type_tuple(true)
		var returns
		if ls.token == ':'
			ls.step()
			returns = type_tuple(false)
		
		lex_match(']', '[', loc.line)
		return ty.func(params, returns)


	var type_prefix = ->
		var loc = ls.loc()
		var typ
		if ls.token == 'TK_name'
			typ = ty.custom(ls.value)
			ls.step()
		else if ls.token == '('
			ls.step()
			typ = ast.bracket(parse_type())
			lex_match(')', '(', loc.line)
		else
			-- parse_error(3, "invalid type annotation %s", ls.astext(ls.token))
			-- optional typing
			return

		-- Parse multiple type suffix
		while ls.token == '.'  -- type name cannot have `-`, so no need to consider `[`
			ls.step() 
			if ls.token ~= 'TK_name'
				break
			typ = ty.index(typ, ls.value)
			ls.step()  -- skip TK_name
			
		return typ


	type_basic = ->
		var loc = ls.loc()
		var val
		if ls.token == 'TK_name'
			val = ls.value
		var typ
		if val == 'any'
			typ = ty.any()
		else if val == 'num'
			typ = ty.num()
		else if val == 'str'
			typ = ty.str()
		else if val == 'bool'
			typ = ty.bool()
		else
			if ls.token == '['
				return type_func(loc)
			if ls.token == '{'
				return type_tbl(loc)
			return type_prefix()

		ls.step()
		return typ


	type_unary = ->
		var tk = ls.token
		if tk == '$'
			ls.step()
			var t = type_binary(operator.unary_priority)
			return ty.typeof(t)
		else
			return type_basic()


	type_binary = \limit ->
		var l = type_unary()
		var op = ls.token
		while operator.is_typeop(op) and operator.left_priority(op) > limit
			ls.step()
			var r, nextop = type_binary(operator.right_priority(op))
			if op == '?'
				l = ty.or(l, ty.nil())
			else if op == '|'
				l = ty.or(l, r)
			else if op == '&'
				l = ty.and(l, r)
			else
				parse_error(3, "unexpected %s", ls_value() or ls.astext(ls.token))
				break
			op = nextop
		return l, op


	parse_type = \varargs ->
		var typ = type_binary(0) -- Priority 0: parse whole type
		if typ and varargs
			return ast.varargs(typ)
		return typ

	-- read optional type
	var opt_type = \types, n, varargs ->
		var t = parse_type(varargs)
		if t
			if not types
				types = {}
			types[n] = t
		return types


	--- Expressions ------------------------------------------------
	var expr_primary, expr, expr_unop, expr_binop, expr_simple, expr_list, expr_table
	var parse_body, parse_args, parse_block

	var expr_bracket = ->
		ls.step() -- skip '['
		var v = expr()
		lex_check(']')
		return v


	expr_table = \loc ->
		var vks, n = {}, 0
		var dented = false
		lex_check('{')
		while ls.token ~= '}'
			dented = lex_opt_dent(dented)
			-- catch common mistake that mess everything later
			if not dented and ls.token == 'TK_dedent'
				err_symbol()
				ls.step()

			-- in case the table ends immediately
			if ls.token == '}'
				break
			var key
			if ls.token == '['
				key = expr_bracket()
				lex_check('=')
			else if ls.next() == '='
				if ls.token == 'TK_name'
					var name, at = lex_str()
					key = Expr.string(name, false, at)
				-- accept string
				else if ls.token == 'TK_string'
					key = Expr.string(ls.value, false, ls)
					ls.step()
				-- accept keyword
				else
					var name = is_keyword()
					if name
						key = Expr.string(name, false, ls)
					else
						err_syntax("invalid table key " .. ls_value() or ls.astext(ls.token))
					ls.step()
				lex_check('=')
		
			var val = expr()
			n = n + 1
			vks[n] = { val, key }  -- key can be nil

			dented = lex_opt_dent(dented)
			if ls.token == ';'
				err_instead(1, "use `,`")
			if not lex_opt(',') and not lex_opt(';')
				break

		if dented and not lex_dedent()
			err_instead(3, "%s expected to match %s at line %d", ls.astext('TK_dedent'), ls.astext('TK_indent'), loc.line)
		lex_match('}', '{', loc.line)
		return Expr.table(vks, loc)


	var expr_function = \loc ->
		if ls.token == '\\'
			ls.step()
		var curry, params, types, _, retypes, body = parse_body(loc.line)
		var lambda = Expr.function(params, types, retypes, body, loc)
		if curry
			var cargs = { Expr.number(#params, loc), lambda }
			return Expr.call(Expr.id('curry', loc), cargs, loc)
		return lambda


	var parse_variants = \destruct ->
		var variants, v = {}, 0
		var ind = lex_indent()
		do
			var ctor, body
			-- params are optional
			var params, p = {}, 0
			var starred
			if ls.token == 'TK_name' or (not LJ_52 and ls.token == 'TK_goto')
				ctor = Expr.id(lex_str())
			else
				var name = is_keyword()
				if name
					ctor = Expr.id(name, ls)
				else if destruct and ls.token == '*'
					if starred
						err_syntax(ls.astext(ls.token) .. " already defined on line " .. starred)
					starred = ls.line
					ctor = Expr.id(ls.token, ls)
				ls.step()

			if ctor
				-- `:` is optional if no params
				if lex_opt(':')
					do
						if ls.token == 'TK_name' or (not LJ_52 and ls.token == 'TK_goto')
							p = p + 1
							params[p] = Expr.id(lex_str())
						else if ls.token == '...'
							ls.step()
							p = p + 1
							params[p] = Expr.vararg(ls)
							break
					until not lex_opt(',')

				if destruct
					if not lex_opt('->') and p > 0
						err_expect('->')
					body = parse_block(ls.line, '->')

				lex_opt('TK_newline')
				v = v + 1
				variants[v] = {ctor = ctor, params = params, body = body}
			else
				err_symbol()
				ls.step()
				break

			if not ind
				lex_opt(';')
				break
		until lex_dedent()
		if v < 1
			parse_error(3, "at least one constructor needed for disjoint union")
		return variants


	expr_simple = ->
		var tk, val = ls.token, ls.value
		var loc = ls.loc()
		var e
		if tk == 'TK_number'
			e = Expr.number(val, loc)
		else if tk == 'TK_string'
			e = Expr.string(val, false, loc)
		else if tk == 'TK_longstring'
			e = Expr.string(val, true, loc)
		else if tk == 'TK_nil'
			e = Expr.nil(loc)
		else if tk == 'TK_true'
			e = Expr.bool(true, loc)
		else if tk == 'TK_false'
			e = Expr.bool(false, loc)
		else if tk == '...'
			e = Expr.vararg(loc)
		else if tk == '{'
			return expr_table(loc)
		else if tk == '\\' or tk == '->' or tk == '~>'
			return expr_function(loc)
		else if tk == ':!'
			ls.step()
			return Expr.union(parse_variants(false), nil, nil, loc)
		else
			return expr_primary()

		ls.step()
		return e


	expr_list = ->
		var exps = {}
		exps[1] = expr()
		while ls.token == ','
			ls.step()
			exps[#exps + 1] = expr()
		return exps


	expr_unop = ->
		var tk = ls.token
		if tk == 'TK_not' or tk == '-' or tk == '#'
			ls.step()
			var v = expr_binop(operator.unary_priority)
			return Expr.unary(ls.tostr(tk), v, ls)
		else
			return expr_simple()


	-- Parse binary expressions with priority higher than the limit.
	expr_binop = \limit ->
		var v = expr_unop()
		var op = ls.tostr(ls.token)
		while operator.is_binop(op) and operator.left_priority(op) > limit
			ls.step()
			var v2, nextop = expr_binop(operator.right_priority(op))
			v = Expr.binary(op, v, v2, ls)
			op = nextop
		return v, op


	expr = ->
		return expr_binop(0) -- Priority 0: parse whole expression.


	-- Parse primary and prefix expression
	expr_primary = ->
		var v, vk
		if ls.token == '('
			var line = ls.line
			ls.step()
			vk, v = Kind.Expr, ast.bracket(expr())
			lex_match(')', '(', line)
		else
			var str
			var loc = ls.loc()
			if ls.token == 'TK_name' or (not LJ_52 and ls.token == 'TK_goto')
				str, loc = lex_str()
			else
				err_symbol()
			v, vk = Expr.id(str, loc), Kind.Var

		var key
		while true -- Parse multiple expression suffixes
			var at = ls.loc()
			if ls.token == '.'
				ls.step() -- skip dot
				var kw = is_keyword()
				if kw
					key = Expr.string(kw, false, ls)
					vk, v = Kind.Index, Expr.index(v, key, at)
					ls.step()
				else
					key = lex_str()
					vk, v = Kind.Property, Expr.property(v, key, at)
			else if ls.token == '['
				key = expr_bracket()
				vk, v = Kind.Index, Expr.index(v, key, at)
			else if ls.token == '(' -- or ls.token == 'TK_string' or ls.token == '{'
				var args = parse_args()
				vk, v = Kind.Call, Expr.call(v, args, at)
			else if ls.token == ':'
				ls.step()
				var arg = expr()
				lex_check('!')
				vk, v = Kind.Union, Expr.union(parse_variants(true), v, arg, at)
			else
				break
		return v, vk

	--- Statements ------------------------------------------------
	var parse_return = \loc ->
		ls.step() -- skip 'return'
		var exps
		if EndOfBlock[ls.token] or NewLine[ls.token]
			exps = {}
		else -- return with one or more values
			exps = expr_list()
		return Stmt.return(exps, loc)


	-- Parse numeric 'for'
	var parse_for_num = \loc ->
		var var = Expr.id(lex_str())
		lex_check('=')
		var first = expr()
		lex_check(',')
		var last = expr()
		var step
		if lex_opt(',')
			step = expr()
		var body = parse_block(loc.line, 'TK_for')
		return Stmt.fornum(var, first, last, step, body, loc)


	-- Parse 'for' statement
	var parse_for = \loc ->
		ls.step()  -- skip 'for'
		if ls.next() == '='
			return parse_for_num(loc)

		-- Parse 'for .. in ..'
		var vars, n = {}, 0
		var types
		do
			n = n + 1
			vars[n] = Expr.id(lex_str())
			types = opt_type(types, n)
		until not lex_opt(',')
		lex_check('TK_in')
		var exps = expr_list()
		var body = parse_block(loc.line, 'TK_for')
		return Stmt.forin(vars, types, exps, body, loc)


	-- Parse function call argument list
	parse_args = ->
		var line = ls.line
		lex_check('(')
		if not LJ_52 and line ~= ls.prevline
			err_warn("ambiguous syntax (function call x new statement)")

		var dented = false
		var args, a = {}, 0
		while ls.token ~= ')'
			dented = lex_opt_dent(dented)
			-- catch common mistake that mess everything later
			if not dented and ls.token == 'TK_dedent'
				err_symbol()
				ls.step()
			if ls.token == ')'
				break
			a = a + 1
			args[a] = expr()

			dented = lex_opt_dent(dented)
			if not lex_opt(',')
				break
			
		if dented and not lex_dedent()
			err_instead(3, "%s expected to match %s at line %d", ls.astext('TK_dedent'), ls.astext('TK_indent'), line)
		lex_match(')', '(', line)

		return args


	var parse_assignment
	parse_assignment = \lhs, v, vk ->
		if vk ~= Kind.Var and vk ~= Kind.Property and vk ~= Kind.Index
			err_symbol()

		var loc = ls.loc()
		lhs[#lhs+1] = v
		if lex_opt(',')
			var n_var, n_vk = expr_primary()
			return parse_assignment(lhs, n_var, n_vk)
		-- Parse RHS
		lex_check('=')
		var exps = expr_list()
		return Stmt.assign(lhs, exps, loc)


	var parse_call_assign = \loc ->
		var v, vk = expr_primary()
		if vk == Kind.Call or vk == Kind.Union
			return Stmt.expression(v, loc)
		var lhs = {}
		return parse_assignment(lhs, v, vk)


	var parse_var = \loc ->
		var lhs, i = {}, 0
		var types
		do
			i = i + 1
			lhs[i] = Expr.id(lex_str())
			types = opt_type(types, i)
		until not lex_opt(',')

		var rhs = {}
		if lex_opt('=')
			rhs = expr_list()
		return Stmt.local(lhs, types, rhs, loc)


	var parse_while = \loc ->
		ls.step() -- skip 'while'
		var cond = expr()
		var body = parse_block(loc.line, 'TK_while')
		return Stmt.while(cond, body, loc)


	var parse_then = \tests, line ->
		ls.step() -- skip 'if'
		tests[#tests+1] = expr()
		if ls.token == 'TK_then'
			err_warn("`then` is not needed")
			ls.step()
		return parse_block(line, 'TK_if')


	var parse_if = \loc ->
		var tests, blocks = {}, {}
		blocks[#blocks+1] = parse_then(tests, ls.line)
		var else_branch
		while ls.token == 'TK_else' or NewLine[ls.token] and ls.next() == 'TK_else'
			lex_opt('TK_newline')
			ls.step()
			if ls.token == 'TK_if'
				blocks[#blocks+1] = parse_then(tests, ls.line)
			else
				else_branch = parse_block(ls.line, 'TK_else')
				break
		return Stmt.if(tests, blocks, else_branch, loc)


	var parse_do = \loc ->
		ls.step() -- skip 'do'
		var body = parse_block(loc.line, 'TK_do')
		if lex_opt('TK_until')
			var cond = expr() -- until condition
			return Stmt.repeat(cond, body, loc)
		return Stmt.do(body, loc)


	var parse_break = \loc ->
		ls.step()
		return Stmt.break(loc)


	var parse_label = \loc ->
		ls.step() -- skip '::'
		var name = lex_str()
		lex_check('::')
		return Stmt.label(name, loc)


	var parse_goto = \loc ->
		var name = lex_str()
		return Stmt.goto(name, loc)


	-- Parse one statement
	-- Returns the statement AST and a bool to indicate if it must be the last one in a chunk
	var parse_stmt
	parse_stmt = ->
		var loc = ls.loc()
		var stmt
		if ls.token == 'TK_if'
			stmt = parse_if(loc)
		else if ls.token == 'TK_for'
			stmt = parse_for(loc)
		else if ls.token == 'TK_while'
			stmt = parse_while(loc)
		else if ls.token == 'TK_do'
			stmt = parse_do(loc)
		else if ls.token == 'TK_repeat'
			err_symbol()
			stmt = parse_do(loc)
		--else if ls.token == 'TK_function'
		--	stmt = parse_func()
		else if ls.token == '\\' or ls.token == '->' or ls.token == '~>'
			err_warn("lambda must either be assigned or immediately invoked")
			stmt = expr_function(loc)
		else if ls.token == 'TK_name' and ls.value == 'var' and ls.next() == 'TK_name'
			ls.step()
			stmt = parse_var(loc)
		else if ls.token == 'TK_local'
			err_symbol()
			ls.step()
			stmt = parse_var(loc)
		else if ls.token == 'TK_return'
			stmt = parse_return(loc)
			return stmt, true -- Must be last
		else if ls.token == 'TK_break'
			stmt = parse_break(loc)
			return stmt, not LJ_52 -- Must be last in Lua 5.1
		else if ls.token == '::'
			stmt = parse_label(loc)
		else if ls.token == 'TK_goto'
			if LJ_52 or ls.next() == 'TK_name'
				ls.step()
				stmt = parse_goto(loc)
			-- else fall thru

		-- is TK_name, must be function call or assignment
		if not stmt
			stmt = parse_call_assign(loc)
		return stmt, false


	-- Parse list of statements
	var parse_stmts = ->
		var stmt, islast = nil, false
		var body, b = {}, 0
		while not islast and not EndOfBlock[ls.token]
			stmted = ls.line
			skip_ends()
			stmt, islast = parse_stmt()
			b = b + 1
			body[b] = stmt
			skip_ends()
			-- disallow multiple statement in one line
			if stmted == ls.line
				if ls.token ~= 'TK_eof' and ls.token ~= 'TK_dedent' and ls.next() ~= 'TK_eof'
					err_instead(3, "statement should end. %s expected", ls.astext('TK_newline'))
		return body


	-- Parse indented single or multiple statements
	parse_block = \line, match_token ->
		var body
		if lex_indent()
			body = parse_stmts()
			if not lex_dedent()
				err_instead(3, "%s expected to end %s at line %d", ls.astext('TK_dedent'), ls.astext(match_token), line)
		else
			if not EndOfBlock[ls.token] and not NewLine[ls.token]
				body = { (parse_stmt()) }  -- single line statement, parenthesis needed coz parse_stmt() returns 2 values

			if EndOfBlock[ls.token] or NewLine[ls.token]
				lex_opt(';')
			else
				err_instead(3, "statement should end near %s. %s expected", ls.astext(match_token), ls.astext('TK_newline'))
		return body or {}


	-- Parse function parameters
	var parse_params = ->
		var params, n = {}, 0
		var ptypes, rtypes
		var varargs = false

		if ls.token ~= '->' and ls.token ~= '~>'
			do
				if ls.token == 'TK_name' or (not LJ_52 and ls.token == 'TK_goto')
					n = n + 1
					params[n] = Expr.id(lex_str())
					ptypes = opt_type(ptypes, n)

				else if ls.token == '...'
					ls.step()
					varargs = true
					n = n + 1
					params[n] = Expr.vararg(ls)
					ptypes = opt_type(ptypes, n, true)

					if ls.next() ~= ':'
						break
				else if ls.token == ':'
					ls.step()  -- skip ':'
					var r = 1
					do
						rtypes = opt_type(rtypes, r)
						r = r + 1
					until not lex_opt(',')
					break
				else
					err_instead(2, "parameter expected in function declaration")
			until not lex_opt(',') and ls.token ~= ':'

		var curry = false
		if ls.token == '->'
			ls.step()
		else if ls.token == '~>'
			ls.step()
			curry = true
			if varargs
				err_warn("cannot curry variadic parameters with `~>`")
			if n < 2
				err_warn("at least 2 parameters needed with `~>`")
		else
			err_expect('->')
		if ptypes
			ptypes = ty.tuple(ptypes)
		if rtypes
			rtypes = ty.tuple(rtypes)
		return curry, params, ptypes, varargs, rtypes
		


	-- Parse function body
	parse_body = \line ->
		var curry, params, types, varargs, retypes = parse_params()
		var body = parse_block(line, '->')
		return curry, params, types, varargs, retypes, body



	ls.step()
	lex_opt('TK_newline')
	var chunk = parse_stmts()
	if ls.token ~= 'TK_eof'
		err_syntax("code should end. unexpected extra " .. ls.astext(ls.token))
	return chunk



